// Top-level build file where you can add configuration options common to all sub-projects/modules.

import groovy.json.JsonSlurper

buildscript {
    ext {
        buildToolsVersion = "30.0.2"
        minSdkVersion = 27
        compileSdkVersion = 31
        targetSdkVersion = 31
        ndkVersion = "21.4.7075529"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.2.2")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        maven { url 'https://www.jitpack.io' }
    }
}

def getNodePackageInfo() {
    def packageFile = new File("$rootDir/../package.json")
    def packageJson = new JsonSlurper().parseText(packageFile.text)
    project.logger.info('Version: ' + packageJson["version"])
    return [packageJson["name"], packageJson["version"], packageJson["build"]]
}

subprojects {
    ext {
        apkName = getNodePackageInfo()[0] + "_" + getNodePackageInfo()[1] + "_" + getNodePackageInfo()[2]
        nodePackageVersion = getNodePackageInfo()[1]
        nodePackageBuild = getNodePackageInfo()[2]
    }
}
